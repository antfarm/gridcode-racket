#lang gridcode

(program
 "Ant"

 (define grid-size 32)
 (define frame-rate 10)

 (define (setup-grid)
   (define x (quotient grid-size 2))
   (define y x)
   (set-cell! x y 'ant (random-direction))
   (set-cell! x y 'trail 1.0))

 (define (update-grid)
   (decay-trails)

   (define idle-ticks (get-grid 'idle-ticks 0))
   (when (and (> idle-ticks 50) (< (random 100) 3))
     (update-ant (random-direction)))
   (set-grid! 'idle-ticks (+ idle-ticks 1))

   (move-ant))

 (define (color-for-cell x y)
   (cond
     ((get-cell x y 'ant) (color 1.0 0.8 0.2))
     ((get-cell x y 'trail) (begin
                              (define concentration (get-cell x y 'trail))
                              (define opacity (min 1.0 (* concentration 0.5)))
                              (color 0.2 0.9 0.0 opacity)))
     (else (color 0.0 0.0 0.0))))

 (define (info-for-cell x y)
   (format "(~a,~a) ~a" x y (get-cell x y)))

 (define (handle-cell-tapped x y)
   (void))

 (define (handle-key-pressed key)
   (cond
     ((member key '(left right up down))
      (begin
        (update-ant key)
        (set-grid! 'idle-ticks 0)))
     ((eq? key #\space) (begin (clear!) (setup-grid)))
     (else (void))))

 (define (update-ant direction)
   (define-list (x y _) (get-any-cell 'ant))
   (set-cell! x y 'ant direction))

 (define (move-ant)
   (define-list (x y direction) (get-any-cell 'ant))
   (define-list (dx dy)
     (case direction
       ((left)  (list -1  0))
       ((right) (list  1  0))
       ((up)    (list  0 -1))
       ((down)  (list  0  1))))
   (define new-x (modulo (+ x dx) grid-size))
   (define new-y (modulo (+ y dy) grid-size))
   (delete-cell! x y 'ant)
   (set-cell! new-x new-y 'ant direction)

   (define concentration (get-cell new-x new-y 'trail 0))
   (set-cell! new-x new-y 'trail (+ concentration 1.0)))

 (define (random-direction)
   (list-ref '(left right up down) (random 4)))

 (define (decay-trails)
   (define evaporation-rate 0.07)
   (for ((cell (get-all-cells 'trail)))
     (define-list (x y concentration) cell)
     (define new-concentration (* concentration (- 1.0 evaporation-rate)))
     (if (< new-concentration 0.07)
         (delete-cell! x y 'trail)
         (set-cell! x y 'trail new-concentration)))))